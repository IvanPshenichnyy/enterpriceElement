
метод ЗагрузитьПриНажатии(Команда: ОбычнаяКоманда)
    пер Уведомление = новый Уведомление("Загрузка курсов валют", "Оперция запущена")
    Уведомление.Показать()
    
    ЗаписатьКурсыВалют()

    Уведомление.Текст = "Операция выполнена"
    Уведомление.Показать()
;




@НаСервере
@ДоступноСКлиента
статический метод ЗаписатьКурсыВалют() 
    пер Ответ: Строка

    исп ОтветХттп = КлиентHttp.СБазовымUrl("https://www.cbr-xml-daily.ru")
        .ЗапросGet("/daily_json.js")
        .Выполнить()
        
    если ОтветХттп.КодСостояния != 200 
        Ответ = ОтветХттп.Тело.ПрочитатьКакСтроку()    
        возврат 
    ;
    
    знч ДанныеОтвета = СериализацияJson.ПрочитатьСоответствие(ОтветХттп.Тело.ПрочитатьКакСтроку()) как неизвестно
    
    
    пер Валюты = ДанныеОтвета["Valute"]
    пер НаименованияКурсов = новый Массив<Строка>()
    для Элемент из Валюты
        НаименованияКурсов.Добавить(Элемент.Значение["Name"])
    ;   
     
    пер СозданныеВалюты = СозданныеВалюты(НаименованияКурсов)
    
    для Элемент из Валюты
        пер КурсДоллара = Элемент.Значение
        пер ВалютаСсылка: Валюты.Ссылка?
        если СозданныеВалюты.СодержитКлюч(КурсДоллара["Name"]) != Истина
            пер Валюта = новый Валюты.Объект()
            Валюта.Наименование = КурсДоллара["Name"]
            Валюта.СимвольныйКод = КурсДоллара["CharCode"]
            Валюта.ЦифровойКод = КурсДоллара["NumCode"]
            Валюта.Идентификатор = КурсДоллара["ID"]
            Валюта.Записать()
            ВалютаСсылка = Валюта.Ссылка
        иначе
            ВалютаСсылка = СозданныеВалюты[КурсДоллара["Name"]]
        ;
        
        пер Сейчас = Дата.Сейчас()
        знч НоваяЗапись = новый КурсыВалют.Запись(
            Период = Сейчас,
            Валюта = ВалютаСсылка,
            Номинал = КурсДоллара["Nominal"],
            Значение = КурсДоллара["Value"]) 

        КурсыВалют.Записать(НоваяЗапись)
    ;    


;

@НаСервере
статический метод СозданныеВалюты(НаименованияКурсов: ЧитаемыйМассив<Строка>): ЧитаемоеСоответствие<Строка, Валюты.Ссылка>
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Наименование КАК Наименование,
            Ссылка КАК Ссылка
        ИЗ
            Валюты
        ГДЕ
            Наименование В (%НаименованияКурсов)
    }
    
    знч СозданныеВалюты = <Строка, Валюты.Ссылка>{:}

    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
         
        СозданныеВалюты.Вставить(СтрокаРезультата.Наименование, СтрокаРезультата.Ссылка)
    ;
    
    возврат СозданныеВалюты
;